grammar net.immute.ccs.CcsEclipse with org.eclipse.xtext.common.Terminals

generate ccsEclipse "http://www.immute.net/ccs/CcsEclipse"

RuleSet:
	(context = Context?)
	(rules += Rule)*
	;

Context:
	'@context' '(' selector=Selector ')' ';'?
;

Selector:
	Sum '>'?
;

Sum: Product (',' Product)*;

Product: Term+;

Term: Step ('>' Step)*;

Step:
	SingleStep | '(' Sum ')'
;

Rule:
	(Import | Constraint | Property | Nested) ';'?
;

Nested:
	Selector (
		(':' (Import | Constraint | Property)) 
		| '{' Rule* '}')
;

Property:
	ID '=' Value
;


terminal HEXINT : '0x' ('0'..'9'|'a'..'f'|'A'..'F')+;
// TODO interpolants
// TODO a better float here, and maybe discriminate ints vs floats
terminal NUMBER : '-'? ('0'..'9'|'.')+ (('e'|'E') '-'? ('0'..'9')+)?;

bool : 'true' | 'false';

Value:
	HEXINT
	| NUMBER
	| STRING
	| bool
;

Constraint:
	'@constrain' SingleStep
;

SingleStep:
	Ident Vals? StepSuffix?
;

Ident: ID | STRING;

Vals: '.' Ident Vals*;

StepSuffix: '/' SingleStep;

Import: '@import' STRING;


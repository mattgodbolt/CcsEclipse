grammar net.immute.ccs.CcsEclipse with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate ccsEclipse "http://www.immute.net/ccs/CcsEclipse"

RuleSet:
	(context = Context?)
	(rules += Rule)*
	;

Context:
	'@context' '(' selector=Selector ')' ';'?
;

Selector:
	Sum '>'?
;

Sum: Product (',' Product)*;

Product: Term+;

Term: Step ('>' Step)*;

Step:
	SingleStep | '(' Sum ')'
;

Rule:
	(Import | Constraint | Property | Nested) ';'?
;

Nested:
	Selector (
		(':' (Import | Constraint | Property)) 
		| '{' Rule* '}')
;

Modifier: ("@override")?;

Property:
	Modifier ID '=' Value
;


terminal HEXINT returns ecore::EInt: '0x' ('0'..'9'|'a'..'f'|'A'..'F')+;
// TODO interpolants
terminal NUMBER returns ecore::EBigDecimal:
    (('0'..'9')+ ('.' ('0'..'9')*)? | 
    '.' ('0'..'9')+) (('e' | 'E') ('+' | '-')? ('0'..'9')+)?;
    
bool : 'true' | 'false';

Value:
	HEXINT
	| NUMBER
	| STRING
	| bool
;

Constraint:
	'@constrain' SingleStep
;

SingleStep:
	Ident Vals? StepSuffix?
;

Ident: ID | STRING;

Vals: '.' Ident Vals*;

StepSuffix: '/' SingleStep;

Import: '@import' STRING;

